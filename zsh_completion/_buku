#compdef buku
#
# Completion definition for buku.
#
# Author:
#   Arun Prakash Jana <engineerarun@gmail.com>
#

setopt localoptions noshwordsplit noksharrays
local -a args
args=(
    '(-a --add)'{-a,--add}'[add bookmark]:URL tags'
    '(-c --comment)'{-c,--comment}'[comment on bookmark]'
    '(-d --delete)'{-d,--delete}'[delete bookmark]'
    '(--deep)--deep[search matching substrings]'
    '(-e --export)'{-e,--export}'[export bookmarks]:html output file'
    '(--expand)--expand[expand a tny.im shortened URL]:index/shorturl'
    '(-f --format)'{-f,--format}'[limit fields in print and Json output]:value'
    '(-h --help)'{-h,--help}'[show help]'
    '(-i --import)'{-i,--import}'[import bookmarks]:html imput file'
    '(--immutable)--immutable[disable title update from web]:value'
    '(-j --json)'{-j,--json}'[show Json output for print and search]'
    '(-k --unlock)'{-k,--unlock}'[decrypt database]'
    '(-l --lock)'{-l,--lock}'[encrypt database]'
    '(-m --merge)'{-m,--merge}'[merge another buku database]:buku db file'
    '(--markdown)--markdown[markdown mode]'
    '(--nocolor)--nocolor[disable color output]'
    '(--noprompt)--noprompt[noninteractive mode]'
    '(-o --open)'{-o,--open}'[open bookmarks in browser]'
    '(-p --print)'{-p,--print}'[show bookmark details]'
    '(-r --replace)'{-r,--replace}'[replace a tag]:tag to replace'
    '(-s --sany)'{-s,--sany}'[match any keyword]:keyword(s)'
    '(-s --sall)'{-s,--sall}'[match all keywords]:keyword(s)'
    '(--shorten)--shorten[shorten a URL using tny.im]:index/url'
    '(--sreg)--sreg[match a regular exression]:regex'
    '(--stag)--stag[search by tag or show tags]'
    '(-t --title)'{-t,--title}'[set custom title]'
    '(--tacit)--tacit[reduce verbosity]'
    '(--tag)--tag[set tags, use + to append, - to remove]'
    '(--threads)--threads[max connections for full refresh]:value'
    '(-u --update)'{-u,--update}'[update bookmark]'
    '(--upstream)--upstream[check latest upstream release]'
    '(--url)--url[set url]:url'
    '(-v --version)'{-v,--version}'[show program version]'
    '(-z --debug)'{-z,--debug}'[enable debugging mode]'
)
_arguments -S -s $args
